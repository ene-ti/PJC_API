unit u_CapaService;

interface

uses
  System.Classes,
  System.SysUtils,
  System.Generics.Collections,
  u00_conexao,
  u_CapaClass,

  FireDAC.Stan.Intf,
  FireDAC.Stan.Option,
  FireDAC.Stan.Error,
  FireDAC.Stan.Pool,
  FireDAC.Stan.Async,
  FireDAC.Stan.Def,
  FireDAC.UI.Intf,
  FireDAC.Phys.Intf,
  FireDAC.DApt,
  FireDAC.Phys,
  FireDAC.VCLUI.Wait,
  FireDAC.Comp.Client,
  FireDAC.Phys.MySQL, FireDAC.Phys.MySQLDef,                //MySql
  FireDAC.Phys.FBDef, FireDAC.Phys.IBBase, FireDAC.Phys.FB, //Firebirdb
  Data.DB;

type
  TCapaService = class
  private

  public
    class function GetAlbuns(const cField, cWhere, cOrderBy, cRegAtual, cQtdReg: string): TObjectList<TCapa>;
    class function GeTCapa(const cAlb_id: Integer): TCapa;
    class procedure CreateAlbum(const AAlbum: TCapa);
    class procedure UpdateAlbum(const cAlb_id: Integer; const AAlbum: TCapa);
    class procedure DeleteAlbum(const cAlb_id: Integer);
  end;

implementation

uses u00_Global, u00_FunPro;

{ TCapaService }

class function TCapaService.GetAlbuns(const cField, cWhere, cOrderBy, cRegAtual, cQtdReg: string): TObjectList<TCapa>;
var
  FDConexao: TFDConnection;
  TmpDataset: TDataSet;
  AAlbum: TCapa;
  vWhereLike, vOrderBy: string;
  vRegAtual, vQtdReg, vIni, vCont: integer;
begin
  Result := TObjectList<TCapa>.Create;

  FDConexao := TFDConnection.Create(nil);
  try
    vRegAtual := StrToIntDef(cRegAtual,0);
    vQtdReg := StrToIntDef(cQtdReg,0);

    if cWhere.Trim.IsEmpty then
      vWhereLike := ' '
    else
      vWhereLike := ' WHERE ' + cField + ' LIKE ''%' + cWhere + '%'' ';

    if cOrderBy.Trim.IsEmpty then
      vOrderBy := ' ORDER BY ART_ID, ALB_ID '
    else
      vOrderBy := ' ORDER BY ART_NOME, ALB_NOME ' + cOrderBy;

    FDConexao.ConnectionDefName := NOME_CONEXAO_BD;
    FDConexao.ExecSQL('SELECT * FROM ARTISTA ART ' +
                      ' LEFT OUTER JOIN ALBUM ALB ON ART.ART_ID = ALB.ID_ART ' +
                      vWhereLike + vOrderBy, TmpDataset);

    if not TmpDataset.IsEmpty then
    begin
      TmpDataset.First;
      vIni := 0;
      vCont := 0;
      while not TmpDataset.Eof do
      begin
        vIni := vIni+1;
        if (vRegAtual > 0) and (vIni <= vRegAtual) then
        begin
          TmpDataset.Next;
          Continue;
        end;
        vCont := vCont+1;

        AAlbum := TCapa.Create;
        AAlbum.art_id   := TmpDataset.FieldByName('ART_ID').AsInteger;
        AAlbum.art_nome := TmpDataset.FieldByName('ART_NOME').AsString;
        AAlbum.art_categoria := TmpDataset.FieldByName('ART_CATEGORIA').AsString;
        AAlbum.alb_id   := TmpDataset.FieldByName('ALB_ID').AsInteger;
        AAlbum.alb_nome := TmpDataset.FieldByName('ALB_NOME').AsString;
        AAlbum.id_art   := TmpDataset.FieldByName('ID_ART').AsInteger;

        Result.Add(AAlbum);
        TmpDataset.Next;
        if (vQtdReg > 0) and (vCont >= vQtdReg) then
        begin
          break;
        end;
      end;
    end
    else
      raise EDatabaseError.Create('Nenhum Album cadastrado na base de dados!');
  finally
    TmpDataset.Free;
    FDConexao.Free;
  end;
end;

class function TCapaService.GeTCapa(const cAlb_id: Integer): TCapa;
var
  FDConexao: TFDConnection;
  TmpDataset: TDataSet;
begin
  Result := TCapa.Create;

  FDConexao := TFDConnection.Create(nil);
  try
    FDConexao.ConnectionDefName := NOME_CONEXAO_BD;

    FDConexao.ExecSQL('SELECT * FROM ARTISTA ART ' +
                      ' LEFT OUTER JOIN ALBUM ALB ON ART.ART_ID = ALB.ID_ART ' +
                      '  WHERE ALB_ID = ' + cAlb_id.ToString, TmpDataset    );

    if not TmpDataset.IsEmpty then
    begin
        Result.art_id   := TmpDataset.FieldByName('ART_ID').AsInteger;
        Result.art_nome := TmpDataset.FieldByName('ART_NOME').AsString;
        Result.art_categoria := TmpDataset.FieldByName('ART_CATEGORIA').AsString;
        Result.alb_id   := TmpDataset.FieldByName('ALB_ID').AsInteger;
        Result.alb_nome := TmpDataset.FieldByName('ALB_NOME').AsString;
        Result.id_art   := TmpDataset.FieldByName('ID_ART').AsInteger;
    end
    else
      raise EDatabaseError.CreateFmt('Album "%d" não encontrado na base de dados!', [cAlb_id]);
  finally
    TmpDataset.Free;
    FDConexao.Free;
  end;
end;

class procedure TCapaService.CreateAlbum(const AAlbum: TCapa);
var
  FDConexao: TFDConnection;
const
  SQL_INSERT: string =
    'INSERT INTO ALBUM (      ' + sLineBreak +
    '  ALB_NOME, ID_ART       ' + sLineBreak +
    ') VALUES (               ' + sLineBreak +
    '  :ALB_NOME, :ID_ART     ' + sLineBreak +
    ')';
begin
  if AAlbum.alb_nome.Trim.IsEmpty then
    raise EDatabaseError.Create('Nome do Album é obrigatório');
  if AAlbum.id_art = 0 then
    raise EDatabaseError.Create('ID do Artista do Album é obrigatório');

  FDConexao := TFDConnection.Create(nil);
  try
    FDConexao.ConnectionDefName := NOME_CONEXAO_BD;
    FDConexao.ExecSQL(SQL_INSERT,
      [
        AAlbum.alb_nome,
        AAlbum.id_art
      ],
      [
        ftString,
        ftInteger
      ]
    );
  finally
    FDConexao.Free;
  end;
end;

class procedure TCapaService.UpdateAlbum(const cAlb_id: Integer; const AAlbum: TCapa);
var
  FDConexao: TFDConnection;
  CountAtu: Integer;

const
  SQL_UPDATE: string =
    'UPDATE ALBUM SET                ' + sLineBreak +
    '  ALB_NOME = :ALB_NOME,           ' + sLineBreak +
    '  ID_ART = :ID_ART  ' + sLineBreak +
    'WHERE ALB_ID = :ALB_ID            ';
begin
  if AAlbum.alb_nome.Trim.IsEmpty then
    raise EDatabaseError.Create('Nome do Album é obrigatório');
  if AAlbum.id_art = 0 then
    raise EDatabaseError.Create('ID do Artista do Album é obrigatório');

  FDConexao := TFDConnection.Create(nil);
  try
    FDConexao.ConnectionDefName := NOME_CONEXAO_BD;
    CountAtu := FDConexao.ExecSQL(SQL_UPDATE,
      [
        AAlbum.alb_nome,
        AAlbum.id_art,
        cAlb_id
      ],
      [
        ftString,
        ftInteger,
        ftInteger
      ]
    );

    if CountAtu <= 0 then
      raise Exception.Create('Nenhum Album foi atualizado');
  finally
    FDConexao.Free;
  end;
end;

class procedure TCapaService.DeleteAlbum(const cAlb_id: Integer);
var
  FDConexao: TFDConnection;
  CountDelete: Integer;
begin
  FDConexao := TFDConnection.Create(nil);
  try
    FDConexao.ConnectionDefName := NOME_CONEXAO_BD;

    CountDelete := FDConexao.ExecSQL(
      'DELETE FROM ALBUM WHERE ALB_ID = :ALB_ID',
      [cAlb_id],
      [ftInteger]
    );

    if CountDelete = 0 then
      raise EDatabaseError.Create('Nenhum Album foi excluido!');
  finally
    FDConexao.Free;
  end;
end;




end.

